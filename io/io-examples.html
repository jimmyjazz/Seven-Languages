<!--#include virtual="/menu.cgi?path=/about/samplecode" -->


<ul style="margin-left: 0px; width: 50em">


<h3>Hello world</h3>
<p>
<pre>"Hello world!" print
</pre>
</p>

<h3>Factorial</h3>
<p>
<pre>Number factorial := method(n := 1; for(i, 1, self, n = n * i); n)
</pre>
</p>

<h3>Vectorized Factorial</h3>
<p>
<pre>Numberfactorial := method((Vector clone setSize(self) rangeFill += 1) product)
</pre>
</p>

<h3>99 bottles of beer</h3>
<p>
<pre>bottle := method(i,
     if(i == 0, return "no more bottles")
     if(i == 1, return "1 bottle")
     return "" .. i .. " bottles"
)

for(i, 99, 1, -1,
     writeln(bottle(i), " of beer on the wall, ", bottle(i), " of beer,")
     writeln("take one down, pass it around,")
     writeln(bottle(i - 1), " of beer on the wall.")
)
</pre>
</p>

<h3>Create a simple object and show how to use it</h3>
<p>
<pre>Account := Object clone do(
    balance := 0
    deposit  := method(v, balance = balance + v)
    withdraw := method(v, balance = balance - v)
    show := method(writeln("Account balance: $", balance))
)

myAccount := Account clone
myAccount show
"Depositing $10\n" print
myAccount deposit(10)
myAccount show
</pre>
</p>

<h3>Concurrency example - the following will print "112233"</h3>
<p>
<pre>o1 := Object clone
o1 test := method(for(n, 1, 3, n print; yield))
o2 := o1 clone

// @ means send an asynchronous message
o1 @test; o2 @test 

// here we pause the main coroutine
//  the process will exit after async messages are processed and there
// are no yielding coroutines to be switched to

Coroutine currentCoroutine pause
</pre>
</p>

<h3>Look up a class/prototype given its name as a string</h3>
<p>
<pre>Lobby getSlot("PrototypeName")
</pre>
</p>

<h3>Execute a method given its name as a string</h3>
<p>
<pre>anObject perform("SomeMethodName", arg1, arg2)
</pre>
</p>

<h3>Find out if a method with a given name (again a string) exists</h3>
<p>
<pre>anObject hasSlot("SomeMethodName")
</pre>
</p>

<h3>Given an arbitrary reference to a class/prototype, instantiate it</h3>
<p>
<pre>newInstance := objectRef clone
</pre>
</p>

<h3>Add a method to a primitive</h3>
<p>
<pre>Number double := method(self * 2)
1 double
==> 2
</pre>
</p>

<h3>Subclass a primitive</h3>
<p>
<pre>MyList := List clone // Same as creating an instance
</pre>
</p>

<h3>Iterate through an inheritance hierarchy</h3>
<p>
<pre>Object printAllSlots := method(  
    self slotNames foreach(slotName, 
        writeln(slotName)
        if(self hasSlot("proto"), self proto printAllSlots)
    )
)
Object printAllSlots
</pre>
</p>

<h3>Implement a generic proxy</h3>
<p>
<pre>Proxy = Object clone
Proxy forward := method(
    methodName := call message name
    args := call message argsEvaluatedIn(call sender)

    // When an unrecognized message is received,
    // this method will be invoked.
    // Do what you like with the message here -
    // such as send it over a network, etc.
)
</pre>
</p>

<h3>Implement a singleton</h3>
<p>
<pre>Foo := Object clone
Foo clone := Foo
</pre>
</p>

<h3>Print the source code of a method</h3>
<p>
<pre>myMethod := method(return testing(1+2+3))
getSlot("myMethod") code print
==> "return(testing(1+(2)+(3)))"
</pre>
</p>

<h3>Add an operator method to an object</h3>
<p>
<pre>MyObject := Object clone
MyObject setSlot("+",  method(n, write("adding ", n, "\n")))
MyObject + 123
==> "adding 123"
</pre>
</p>

<h3>Mixins and Aspects</h3>
<p>
<pre>Mix := Object clone
Mix logData := Buffer clone
Mix init := method(logData = logData clone)
Mix log := method(s, logData append(s))

a := Object clone
a appendProto(Mix clone) // Now a also inherits the state and behavior of Mix

b := Object clone
b appendProto(Mix clone) // Now multiple objects use it so it's an "aspect"
</pre>
</p>

<h3>An auto source file importer</h3>
<p>
<pre>Object searchPaths = List clone append(launchPath)

Object forward := method(
    methodName := call message name
    if (methodName characterAt(0) isUpperCase,
        searchPaths foreach(searchPath,
            fileName := Path with(searchPath, methodName .. ".io")
            sourceFile := File clone setPath(fileName)
            if (sourceFile exists) then(
                Lobby doFile(path)
                return Lobby getSlot(methodName)
            )
        )
    )
    Exception raise("Importer: Could not find slot for " .. methodName)
)
</pre>
</p>

<h3>Fetch a URL and find the links in it</h3>
<p>
<pre>aString := URL with("http://www.yahoo.com/") fetch
links := aString asXML elementsWithName("a") map(attributes at("href"))
</pre>
</p>
 
<!--
<h3>Fetch and inspect an RSS feed</h3>
<p>
<pre>aString := URL with("http://www.iolanguage.com/blog/blog.cgi?do=rss") fetch
items := aString asXML asObject xml rss channel items
items foreach(item, writeln(item title))
</pre>
</p>
-->
 
<h3>A simple whois client</h3>
<p>
<pre>whois := method(host,
	socket := Socket clone setHostName("rs.internic.net") setPort(43) 
	socket connect streamWrite(host, "\n")
	while(socket streamReadNextChunk, nil) // sockets auto yield to other coros
	return socket readBuffer
)
</pre>
</p>

<h3>A minimal web server</h3>
<p>
<pre>WebRequest := Object clone do(
	handleSocket := method(aSocket,
		aSocket streamReadNextChunk
		request := aSocket readBuffer betweenSeq("GET ", " HTTP")
		f := File with(request) 
		if(f exists, f streamTo(aSocket), aSocket streamWrite("not found"))
		aSocket close
	)
)

WebServer := Server clone do(
	setPort(8000)
	handleSocket := method(aSocket, 
		WebRequest clone @handleSocket(aSocket)
	)
)

WebServer start
</pre>

<h3>OpenGL</h3>
<p>
See the <a href="http://github.com/stevedekorte/io/tree/master/addons/OpenGL/docs/tutorial/">Io versions of the NeHe OpenGL tutorials</a>.
</p>

</div>

</td></tr></table>

</body>
</html>
